/*
(C) 2022-2023 Серый MLGamer <Seriy-MLGamer@yandex.ru>

This file is part of Pixic.
Pixic is free software: you can redistribute it and/or modify it under the terms of the GNU General Public License as published by the Free Software Foundation, either version 3 of the License, or (at your option) any later version.
Pixic is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU General Public License for more details.
You should have received a copy of the GNU General Public License along with Pixic. If not, see <https://www.gnu.org/licenses/>.
*/

/**
 * @file core/strings/file name.pixi
 *
 * Dependencies.
 * configuration.pixi
 */

/**
 * Removes unused slashes and replaces them to the operating system default slashes.
 * @param $path a file or folder path.
 */
fn format_path($path)
{
	if $path<0
	{
		ret(1)
	}
	$pointer=0
	$slashes=0
	for ($a=0; $path[$a]; $a+1)
	{
		$char=$path[$a]
		if $slashes
		{
			if $char!='/'&&$char!='\\'
			{
				$slashes=0
				$path[$pointer]=$char
				$pointer+1
			}
		}
		else
		{
			if $char!='/'&&$char!='\\'
			{
				$path[$pointer]=$char
			}
			else
			{
				if SHELL==SHELL_BASH
				{
					$path[$pointer]='/'
				}
				else{if SHELL==SHELL_CMD
				{
					$path[$pointer]='\\'
				}}
				$slashes=1
			}
			$pointer+1
		}
	}
	if $pointer
	{
		$path[$pointer]=0
		ret(0)
	}
	ret(2)
}
fn get_full_file_name($path)
{
	if $path<0
	{
		ret(-1)
	}
	$result=clone($path)
	$a=strlen($result)-1 while !($result[$a]!='/'&&$result[$a]!='\\')
	{
		$a-1
	}
	if $a!=-1
	{
		resize($result, $a+1)
		while $a!=-1&&$result[$a]!='/'&&$result[$a]!='\\'
		{
			$a-1
		}
		if $a!=-1
		{
			$a+1
			copy($result, $result, 0, $a)
			resize($result, get_size($result)-$a)
		}
		$result.full_file_name=1
		ret($result)
	}
	remove($result)
	ret(-2)
}
fn get_file_name($path)
{
	if $path<0
	{
		ret(-1)
	}
	if $path.full_file_name
	{
		$result=clone($path)
	}
	else
	{
		$result=get_full_file_name($path)
		if $result<0
		{
			ret($result)
		}
	}
	$end=get_size($result)-1
	$a=$end while $a&&$result[$a]!='.'
	{
		$a-1
	}
	if $a&&$a!=$end
	{
		resize($result, $a)
	}
	ret($result)
}
fn get_file_extension($path)
{
	if $path<0
	{
		ret(-1)
	}
	if $path.full_file_name
	{
		$result=clone($path)
	}
	else
	{
		$result=get_full_file_name($path)
		if $result<0
		{
			ret($result)
		}
	}
	$end=get_size($result)-1
	$a=$end while $a&&$result[$a]!='.'
	{
		$a-1
	}
	if $a&&$a!=$end
	{
		$a+1
		copy($result, $result, 0, $a)
		resize($result, $end+1-$a)
		ret($result)
	}
	remove($result)
	ret(-3)
}
/**
 * Standard get_real_path() function gets the path relative to the path of the booted Pixi-program. This function gets it relative to the working directory of the Pixilang interpreter.
 * @param $path a file or folder path.
 * @return an ID of the container with the real file path string (remove it with remove()); -1 if $path value is negative; -2 if path is wrong.
 */
fn get_real_path2($path)
{
	$start=$path[0]
	if ($start<'A'||$start>'Z')&&($start<'a'||$start>'z')||$path[1]!=':'&&$start!='/'&&$start!='\\'
	{
		$result=clone(WORK_PATH)
		strcat($result, $path)
		ret($result)
	}
	else
	{
		ret(clone($path))
	}
}