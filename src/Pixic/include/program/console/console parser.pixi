/*
(C) 2022-2023 Серый MLGamer <Seriy-MLGamer@yandex.ru>

This file is part of Pixic.
Pixic is free software: you can redistribute it and/or modify it under the terms of the GNU General Public License as published by the Free Software Foundation, either version 3 of the License, or (at your option) any later version.
Pixic is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU General Public License for more details.
You should have received a copy of the GNU General Public License along with Pixic. If not, see <https://www.gnu.org/licenses/>.
*/

/**
 * @file program/console/console parser.pixi
 * Console parser provides command line interface.
 *
 * Include before.
 * configuration.pixi
 * program/console/system/make arguments.pixi
 * Dependencies.
 * core/strings/file name.pixi
 * program/info/warnings.pixi
 * program/info/errors.pixi
 * core/strings/find.pixi
 */

input=-1
output=-1
folder=-1
translate_only=0
show_only=0

STATE_NONE=  0
STATE_OUTPUT=1
STATE_FOLDER=2

jump0=new(128, 1, INT)
clean(jump0)
jump0['-']=handle_extended_command
jump0['h']=help
jump0['l']=license
jump0['o']=set_output
jump0['f']=set_folder
jump0['t']=set_translate_only
jump0['s']=set_show_only

argument_state=STATE_NONE
for (a=1; a!=argument_count; a+1)
{
	argument=arguments[a]
	if argument_state
	{
		if argument_state==STATE_OUTPUT
		{
			if output<0
			{
				output=get_real_path2(argument)
			}
			else
			{
				warning_double_output_single(a)
			}
		}
		else{if argument_state==STATE_FOLDER
		{
			if folder<0
			{
				folder=get_real_path2(argument)
			}
			else
			{
				warning_double_folder_single(a)
			}
		}}
		argument_state=STATE_NONE
	}
	else{if argument[0]=='-'
	{
		jump=jump0[argument[1]]
		if jump
		{
			argument_state=jump(a)
		}
		else
		{
			warning_unknown_parameter(a)
		}
	}
	else{if input<0
	{
		input=get_real_path2(argument)
	}
	else
	{
		warning_double_input(a)
	}}}
}
remove(jump0)

if input<0
{
	error_undefined_input()
}
else{if !translate_only&&output<0||folder<0
{
	extension=get_file_extension(input)
	if strcmp_no_case(extension, "pixi")&&strcmp_no_case(extension, "txt")||extension<0
	{
		name=get_full_file_name(input)
	}
	else
	{
		name=get_file_name(input)
	}
	if name<0
	{
		error_invalid_input()
	}
	remove(extension)
	if !translate_only&&output<0
	{
		output=get_real_path2(name)
		if OS==OS_WINDOWS
		{
			strcat(output, ".exe")
		}
	}
	if folder<0
	{
		if translate_only
		{
			folder=get_real_path2(name)
		}
		else
		{
			folder=clone(PIXIC_TEMP_PATH)
			strcat(folder, name)
		}
	}
	remove(name)
}}

fn handle_extended_command($arg)
{
	$argument=arguments[$arg]
	if !strcmp($argument, 2, "help")
	{
		help()
	}
	else{if !strcmp($argument, 2, "license")
	{
		license()
	}
	else{if strstr($argument, 2, "output=")==2
	{
		if $argument[9]
		{
			if output<0
			{
				$temp=new(get_size($argument)-9, 1, INT8)
				copy($temp, $argument, 0, 9)
				output=get_real_path2($temp)
				remove($temp)
			}
			else
			{
				warning_double_output_extended($arg)
			}
		}
		else
		{
			warning_undefined_parameter($arg)
		}
	}
	else{if strstr($argument, 2, "folder=")==2
	{
		if $argument[9]
		{
			if folder<0
			{
				$temp=new(get_size($argument)-9, 1, INT8)
				copy($temp, $argument, 0, 9)
				folder=get_real_path2($temp)
				remove($temp)
			}
			else
			{
				warning_double_folder_extended($arg)
			}
		}
		else
		{
			warning_undefined_parameter($arg)
		}
	}
	else{if !strcmp($argument, 2, "translate-only")
	{
		set_translate_only()
	}
	else{if !strcmp($argument, 2, "show-only")
	{
		set_show_only()
	}
	else
	{
		warning_unknown_parameter($arg)
	}}}}}}
	ret(STATE_NONE)
}
fn help()
{
	printf(text_help, PIXIC_TEMP_PATH)
	exit(0)
}
fn license()
{
	printf(text_license)
	exit(0)
}
fn set_output($arg)
{
	$argument=arguments[$arg]
	if $argument[2]
	{
		if output<0
		{
			$temp=new(get_size($argument)-2, 1, INT8)
			copy($temp, $argument, 0, 2)
			output=get_real_path2($temp)
			remove($temp)
		}
		else
		{
			warning_double_output_extended($arg)
		}
		ret(STATE_NONE)
	}
	ret(STATE_OUTPUT)
}
fn set_folder($arg)
{
	$argument=arguments[$arg]
	if $argument[2]
	{
		if folder<0
		{
			$temp=new(get_size($argument)-2, 1, INT8)
			copy($temp, $argument, 0, 2)
			folder=get_real_path2($temp)
			remove($temp)
		}
		else
		{
			warning_double_folder_extended($arg)
		}
		ret(STATE_NONE)
	}
	ret(STATE_FOLDER)
}
fn set_translate_only()
{
	translate_only=1
	ret(STATE_NONE)
}
fn set_show_only()
{
	show_only=1
	ret(STATE_NONE)
}