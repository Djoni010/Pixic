/*
(C) 2022-2023 Серый MLGamer <Seriy-MLGamer@yandex.ru>

This file is part of Pixic.
Pixic is free software: you can redistribute it and/or modify it under the terms of the GNU General Public License as published by the Free Software Foundation, either version 3 of the License, or (at your option) any later version.
Pixic is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU General Public License for more details.
You should have received a copy of the GNU General Public License along with Pixic. If not, see <https://www.gnu.org/licenses/>.
*/

/**
 * @file program/translator/parser/parsing functions.pixi
 * 
 * Dependencies.
 * program/translator/Text.pixi
 */

fn parse_multiple($parsers_array, $text)
{
	$size=get_size($parsers_array)
	for ($a=0; $a!=$size; $a+1)
	{
		$result=$parsers_array[$a]($text)
		if $result>=0
		{
			ret($result)
		}
	}
	ret(-1)
}
fn skip_spaces($text)
{
	$lines_count=get_ysize($text.lines)
	$state=0
	while 1
	{
		if $text[1, $text.top]==$lines_count
		{
			ret(1)
		}
		Text_promise_position($text)
		$state=skip_spaces_jump0[$state](Text_get($text), $text)
		if $state<0
		{
			ret(0)
		}
	}
}
skip_spaces_jump0=new(5, 1, INT)
skip_spaces_jump0[0]=
{
	if $1=='/'
	{
		ret(1)
	}
	if is_space($1)
	{
		Text_resolve_position($2)
		ret(0)
	}
	Text_reject_position($2)
	ret(-1)
}
skip_spaces_jump0[1]=
{
	if $1=='/'
	{
		Text_resolve_position($2)
		Text_resolve_position($2)
		ret(2)
	}
	if $1=='*'
	{
		Text_resolve_position($2)
		Text_resolve_position($2)
		ret(3)
	}
	Text_reject_position($2)
	Text_reject_position($2)
	ret(-1)
}
skip_spaces_jump0[2]=
{
	Text_resolve_position($2)
	if $1!=EOF
	{
		ret(2)
	}
	ret(0)
}
skip_spaces_jump0[3]=
{
	Text_resolve_position($2)
	if $1!='*'
	{
		ret(3)
	}
	ret(4)
}
skip_spaces_jump0[4]=
{
	Text_resolve_position($2)
	if $1=='/'
	{
		ret(0)
	}
	if $1!='*'
	{
		ret(3)
	}
	ret(4)
}