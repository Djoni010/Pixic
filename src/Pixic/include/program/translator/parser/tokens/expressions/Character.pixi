/*
Copyright (C) 2022 Серый MLGamer <Seriy-MLGamer@yandex.ru>

This file is part of Pixic.
Pixic is free software: you can redistribute it and/or modify it under the terms of the GNU General Public License as published by the Free Software Foundation, either version 3 of the License, or (at your option) any later version.
Pixic is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU General Public License for more details.
You should have received a copy of the GNU General Public License along with Pixic. If not, see <https://www.gnu.org/licenses/>.
*/

/**
 * @file Character.pixi
 *
 * Dependencies.
 * program/translator/parser/parsing functions.pixi
 * program/info/syntax errors.pixi
 * program/translator/parser/tokens/show.pixi
 */

fn Character_new($text, $positions)
{
	$self=new()
	$self.f=Character

	promise_position($positions)
	$self.text=$text.data
	if get_char($text, $positions)!='\''
	{
		reject_position($positions)

		remove($self)
		ret(-1)
	}
	$self.character_start=Stack_get($positions).index
	$backslash=0
	while 1
	{
		$char=get_char($text, $positions)
		if $backslash
		{
			$backslash=0
		}
		else{if $char=='\''
		{
			$self.character_length=Stack_get($positions).index-1-$self.character_start
			resolve_position($positions)

			ret($self)
		}
		else{if $char=='\\'
		{
			$backslash=1
		}
		else{if $char==EOF
		{
			if Stack_get($positions).line==get_ysize($text.lines)
			{
				Stack_get($positions).line-1
				syntax_error_expected($text, $positions, '\'')
			}
		}}}}
	}
}
Character=new()
Character.remove=Character_remove
Character.show=Character_show
fn Character_remove($self)
{
	remove($self)
}
fn Character_show($self)
{
	begin_object()
		indent() logf("\"type\": \"Character\"") next_property()
		indent()
			$character=new($self.character_length, 1, INT8)
			copy($character, $self.text, 0, $self.character_start)
			logf("\"character\": \"%s\"", $character)
			remove($character)
	end_object()
}