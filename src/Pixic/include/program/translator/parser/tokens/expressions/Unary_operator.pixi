/*
Copyright (C) 2022 Серый MLGamer <Seriy-MLGamer@yandex.ru>

This file is part of Pixic.
Pixic is free software: you can redistribute it and/or modify it under the terms of the GNU General Public License as published by the Free Software Foundation, either version 3 of the License, or (at your option) any later version.
Pixic is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU General Public License for more details.
You should have received a copy of the GNU General Public License along with Pixic. If not, see <https://www.gnu.org/licenses/>.
*/

/**
 * @file program/translator/parser/tokens/expressions/Unary_operator.pixi
 *
 * Dependencies.
 * program/translator/parser/parsing functions.pixi
 * program/info/syntax errors.pixi
 * program/translator/parser/parsers groups.pixi
 * program/translator/parser/tokens/show.pixi
 */

fn Unary_operator_new($text, $positions)
{
	$self=new()
	$self.f=Unary_operator

	promise_position($positions)
	$token=get_char($text, $positions)
	if $token=='!'
	{
		$self.operator=OPERATOR_not
	}
	else{if $token=='~'
	{
		$self.operator=OPERATOR_bnot
	}
	else{if $token=='-'
	{
		$self.operator=OPERATOR_neg
	}
	else
	{
		reject_position($positions)

		remove($self)
		ret(-1)
	}}}
	if skip_spaces($text, $positions)
	{
		Stack_get($positions).line-1
		syntax_error_expected_operand($text, $positions)
	}
	$token=parse_multiple(operands, $text, $positions)
	if $token<0
	{
		syntax_error_expected_operand($text, $positions)
	}
	$self.operand=$token
	resolve_position($positions)

	ret($self)
}
Unary_operator=new()
Unary_operator.remove=Unary_operator_remove
Unary_operator.show=Unary_operator_show
fn Unary_operator_remove($self)
{
	$self.operand.f.remove($self.operand)

	remove($self)
}
fn Unary_operator_show($self)
{
	begin_object()
		indent() logf("\"type\": \"Unary_operator\"") next_property()
		indent() logf("\"operator\": \"%s\"", operator_strings[$self.operator]) next_property()
		indent() logf("\"operand\": ") $self.operand.f.show($self.operand)
	end_object()
}