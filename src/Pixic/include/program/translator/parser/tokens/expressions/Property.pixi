/*
Copyright (C) 2022 Серый MLGamer <Seriy-MLGamer@yandex.ru>

This file is part of Pixic.
Pixic is free software: you can redistribute it and/or modify it under the terms of the GNU General Public License as published by the Free Software Foundation, either version 3 of the License, or (at your option) any later version.
Pixic is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU General Public License for more details.
You should have received a copy of the GNU General Public License along with Pixic. If not, see <https://www.gnu.org/licenses/>.
*/

/**
 * @file Property.pixi
 *
 * Dependencies.
 * program/translator/parser/parsing functions.pixi
 * program/info/syntax errors.pixi
 * program/translator/parser/tokens/show.pixi
 */

fn Property_new($text, $positions)
{
	$self=Postfix_new()
	$self.f=Property

	promise_position($positions)
	$self.text=$text.data
	if get_char($text, $positions)!='.'
	{
		reject_position($positions)

		remove($self)
		ret(-1)
	}
	if skip_spaces($text, $positions)
	{
		Stack_get($positions).line-1
		syntax_error_expected_property($text, $positions)
	}
	$self.name_start=Stack_get($positions).index
	promise_position($positions)
	$char=get_char($text, $positions)
	if $char>='0'&&$char<='9'
	{
		reject_position($positions)
		reject_position($positions)

		remove($self)
		ret(-2)
	}
	if is_symbolic($char)
	{
		resolve_position($positions)
		promise_position($positions)
		while 1
		{
			if is_symbolic(get_char($text, $positions))
			{
				resolve_position($positions)
				promise_position($positions)
			}
			else
			{
				reject_position($positions)
				$self.name_length=Stack_get($positions).index-$self.name_start
				resolve_position($positions)

				ret($self)
			}
		}
	}
	reject_position($positions)
	syntax_error_expected_property($text, $positions)
}
Property=new()
Property.remove=Postfix_remove
Property.show=Property_show
fn Property_show($self)
{
	begin_object()
		indent() logf("\"type\": \"Property\"") next_property()
		indent() logf("\"ID\": ") $self.ID.f.show($self.ID) next_property()
		indent()
			$name=new($self.name_length, 1, INT8)
			copy($name, $self.text, 0, $self.name_start)
			logf("\"name\": \"%s\"", $name)
			remove($name)
	end_object()
}