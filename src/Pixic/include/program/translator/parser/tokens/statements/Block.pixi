/*
Copyright (C) 2022 Серый MLGamer <Seriy-MLGamer@yandex.ru>

This file is part of Pixic.
Pixic is free software: you can redistribute it and/or modify it under the terms of the GNU General Public License as published by the Free Software Foundation, either version 3 of the License, or (at your option) any later version.
Pixic is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU General Public License for more details.
You should have received a copy of the GNU General Public License along with Pixic. If not, see <https://www.gnu.org/licenses/>.
*/

/**
 * @file program/translator/parser/tokens/statements/Block.pixi
 *
 * Dependencies.
 * core/structures/linked list/Twoended_list.pixi
 * core/structures/linked list/Twoended_node.pixi
 * program/translator/parser/parsers groups.pixi
 */

fn Block_new($text, $positions)
{
	$self=Twoended_list_new()

	promise_position($positions)
	if get_char($text, $positions)!='{'
	{
		reject_position($positions)

		remove($self)
		ret(-1)
	}
	while 1
	{
		if skip_spaces($text, $positions)
		{
			Stack_get($positions).line-1
			syntax_error_expected($text, $positions, '}')
		}
		$statement=parse_multiple(statements, $text, $positions)
		if $statement<0
		{
			promise_position($positions)
			if get_char($text, $positions)!='}'
			{
				reject_position($positions)
				syntax_error_invalid_statement($text, $positions)
			}
			resolve_position($positions)
			resolve_position($positions)

			ret($self)
		}
		Twoended_node_set($statement, $self)
	}
}
fn Block_remove($self)
{
	$a=$self.first while $a>=0
	{
		$next=$a.next
		$a.f.remove($a)
		$a=$next
	}

	remove($self)
}
fn Block_show($self)
{
	$a=$self.first
	if $a<0
	{
		logf("null")
	}
	else
	{
		begin_array()
			while 1
			{
				indent() $a.f.show($a)
				if $a.next<0
				{
					break
				}
					next_property()
				$a=$a.next
			}
		end_array()
	}
}