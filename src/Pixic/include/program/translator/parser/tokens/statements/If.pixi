/*
Copyright (C) 2022 Серый MLGamer <Seriy-MLGamer@yandex.ru>

This file is part of Pixic.
Pixic is free software: you can redistribute it and/or modify it under the terms of the GNU General Public License as published by the Free Software Foundation, either version 3 of the License, or (at your option) any later version.
Pixic is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU General Public License for more details.
You should have received a copy of the GNU General Public License along with Pixic. If not, see <https://www.gnu.org/licenses/>.
*/

/**
 * @file program/translator/parser/tokens/statements/If.pixi
 *
 * Dependencies.
 * program/translator/parser/tokens/statements/Statement.pixi
 * program/translator/parser/parsing functions.pixi
 * program/info/syntax errors.pixi
 * program/translator/parser/tokens/expressions/expression.pixi
 * program/translator/parser/tokens/statements/Block.pixi
 */

fn If_new($text, $positions)
{
	$self=Statement_new($text, $positions)
	$self.f=If

	promise_position($positions)
	$test=new(get_size(KEYWORDS[KEYWORD_if]), 1, INT8)
	clean($test)
	copy($test, $text.data, 0, Stack_get($positions).index)
	if strstr($test, KEYWORDS[KEYWORD_if])
	{
		remove($test)
		reject_position($positions)

		remove($self)
		ret(-1)
	}
	Stack_get($positions).index+get_size($test)
	promise_position($positions)
	if is_symbolic(get_char($text, $positions))
	{
		reject_position($positions)
		reject_position($positions)

		remove($self)
		ret(-2)
	}
	reject_position($positions)
	if skip_spaces($text, $positions)
	{
		Stack_get($positions).line-1
		syntax_error_expected_expression($text, $positions)
	}
	$token=parse_expression($text, $positions)
	if $token<0
	{
		syntax_error_expected_expression($text, $positions)
	}
	$self.condition=$token
	$self.last_line=Stack_get($positions).line+1
	if skip_spaces($text, $positions)
	{
		Stack_get($positions).line-1
		syntax_error_expected($text, $positions, '{')
	}
	$token=Block_new($text, $positions)
	if $token<0
	{
		syntax_error_expected($text, $positions, '{')
	}
	$self.then=$token
	promise_position($positions)
	if skip_spaces($text, $positions)
	{
		reject_position($positions)
		remove($test)
		$self.else=-1
		resolve_position($positions)

		ret($self)
	}
	resize($test, get_size(KEYWORDS[KEYWORD_else]))
	clean($test)
	copy($test, $text.data, 0, Stack_get($positions).index)
	if strstr($test, KEYWORDS[KEYWORD_else])
	{
		reject_position($positions)
		remove($test)
		$self.else=-2
		resolve_position($positions)

		ret($self)
	}
	Stack_get($positions).index+get_size($test)
	remove($test)
	promise_position($positions)
	if is_symbolic(get_char($text, $positions))
	{
		reject_position($positions)
		reject_position($positions)
		$self.else=-3
		resolve_position($positions)

		ret($self)
	}
	reject_position($positions)
	if skip_spaces($text, $positions)
	{
		Stack_get($positions).line-1
		syntax_error_expected($text, $positions, '{')
	}
	$token=Block_new($text, $positions)
	if $token<0
	{
		syntax_error_expected($text, $positions, '{')
	}
	resolve_position($positions)
	$self.else=$token
	resolve_position($positions)

	ret($self)
}
If=new()
If.remove=If_remove
If.show=If_show
fn If_remove($self)
{
	$self.condition.f.remove($self.condition)
	Block_remove($self.then)
	if $self.else>=0
	{
		Block_remove($self.else)
	}

	remove($self)
}
fn If_show($self)
{
	begin_object()
		indent() logf("\"type\": \"If\"") next_property()
		indent() Statement_show($self) next_property()
		indent() logf("\"condition\": ") $self.condition.f.show($self.condition) next_property()
		indent() logf("\"then\": ") Block_show($self.then)
		if $self.else>=0
		{
				next_property()
			indent() logf("\"else\": ") Block_show($self.else)
		}
	end_object()
}