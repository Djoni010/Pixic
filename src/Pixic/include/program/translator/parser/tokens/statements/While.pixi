/*
Copyright (C) 2022 Серый MLGamer <Seriy-MLGamer@yandex.ru>

This file is part of Pixic.
Pixic is free software: you can redistribute it and/or modify it under the terms of the GNU General Public License as published by the Free Software Foundation, either version 3 of the License, or (at your option) any later version.
Pixic is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU General Public License for more details.
You should have received a copy of the GNU General Public License along with Pixic. If not, see <https://www.gnu.org/licenses/>.
*/

/**
 * @file program/translator/parser/tokens/statements/While.pixi
 *
 * Dependencies.
 * program/translator/parser/tokens/statements/Statement.pixi
 * program/translator/parser/parsing functions.pixi
 * program/info/syntax errors.pixi
 * program/translator/parser/tokens/expressions/expression.pixi
 * program/translator/parser/tokens/statements/Block.pixi
 */

fn While_new($text, $positions)
{
	$self=Statement_new($text, $positions)
	$self.f=While

	promise_position($positions)
	$test=new(get_size(KEYWORDS[KEYWORD_while]), 1, INT8)
	clean($test)
	copy($test, $text.data, 0, Stack_get($positions).index)
	if strstr($test, KEYWORDS[KEYWORD_while])
	{
		remove($test)
		reject_position($positions)

		remove($self)
		ret(-1)
	}
	Stack_get($positions).index+get_size($test)
	remove($test)
	promise_position($positions)
	if is_symbolic(get_char($text, $positions))
	{
		reject_position($positions)
		reject_position($positions)

		remove($self)
		ret(-2)
	}
	reject_position($positions)
	if skip_spaces($text, $positions)
	{
		Stack_get($positions).line-1
		syntax_error_expected_expression($text, $positions)
	}
	$token=parse_expression($text, $positions)
	if $token<0
	{
		syntax_error_expected_expression($text, $positions)
	}
	$self.condition=$token
	$self.last_line=Stack_get($positions).line+1
	if skip_spaces($text, $positions)
	{
		Stack_get($positions).line-1
		syntax_error_expected($text, $positions, '{')
	}
	$token=Block_new($text, $positions)
	if $token<0
	{
		syntax_error_expected($text, $positions, '{')
	}
	$self.body=$token
	resolve_position($positions)

	ret($self)
}
While=new()
While.remove=While_remove
While.show=While_show
fn While_remove($self)
{
	$self.condition.f.remove($self.condition)
	Block_remove($self.body)

	remove($self)
}
fn While_show($self)
{
	begin_object()
		indent() logf("\"type\": \"While\"") next_property()
		indent() Statement_show($self) next_property()
		indent() logf("\"condition\": ") $self.condition.f.show($self.condition) next_property()
		indent() logf("\"body\": ") Block_show($self.body)
	end_object()
}