/*
Copyright (C) 2022 Серый MLGamer <Seriy-MLGamer@yandex.ru>

This file is part of Pixic.
Pixic is free software: you can redistribute it and/or modify it under the terms of the GNU General Public License as published by the Free Software Foundation, either version 3 of the License, or (at your option) any later version.
Pixic is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU General Public License for more details.
You should have received a copy of the GNU General Public License along with Pixic. If not, see <https://www.gnu.org/licenses/>.
*/

/**
 * @file program/translator/parser/tokens/statements/Procedure.pixi
 *
 * Dependencies.
 * program/translator/parser/tokens/statements/Statement.pixi
 * program/translator/parser/tokens/expressions/operand.pixi
 * program/translator/parser/tokens/show.pixi
 */

fn Procedure_new($text, $positions)
{
	$self=Statement_new($text, $positions)
	$self.f=Procedure

	promise_position($positions)
	$token=parse_operand($text, $positions)
	if $token<0
	{
		reject_position($positions)

		remove($self)
		ret(-1)
	}
	if !$token.is_function
	{
		$token.f.remove($token)
		reject_position($positions)

		remove($self)
		ret(-2)
	}
	$self.function=$token
	$self.last_line=Stack_get($positions).line+1
	resolve_position($positions)

	ret($self)
}
Procedure=new()
Procedure.remove=Procedure_remove
Procedure.show=Procedure_show
fn Procedure_remove($self)
{
	$self.function.f.remove($self.function)

	remove($self)
}
fn Procedure_show($self)
{
	begin_object()
		indent() logf("\"type\": \"Procedure\"") next_property()
		indent() Statement_show($self) next_property()
		$function=$self.function
		indent() logf("\"ID\": ") $function.ID.f.show($function.ID) next_property()
		indent() logf("\"arguments\": ")
			$arguments=$function.arguments
			$arguments_count=$arguments.pointer+1
			if $arguments_count
			{
				begin_array()
					$a=0 while 1
					{
						$argument=$arguments[$a]
						indent() $argument.f.show($argument)
						if $a!=$arguments_count-1
						{
								next_property()
							$a+1
						}
						else
						{
							break
						}
					}
				end_array()
			}
			else
			{
				logf("null")
			}
	end_object()
}