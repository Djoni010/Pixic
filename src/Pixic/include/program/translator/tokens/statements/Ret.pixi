/*
(C) 2022-2023 Серый MLGamer <Seriy-MLGamer@yandex.ru>

This file is part of Pixic.
Pixic is free software: you can redistribute it and/or modify it under the terms of the GNU General Public License as published by the Free Software Foundation, either version 3 of the License, or (at your option) any later version.
Pixic is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU General Public License for more details.
You should have received a copy of the GNU General Public License along with Pixic. If not, see <https://www.gnu.org/licenses/>.
*/

/**
 * @file program/translator/tokens/statements/Ret.pixi
 *
 * Dependencies.
 * program/translator/tokens/statements/Statement.pixi
 * program/translator/tokens/expressions/parenthesis.pixi
 */

fn Ret_new($text)
{
	$self=Statement_new($text)
	$self.f=Ret

	Text_promise_position($text)
	$test=new(get_size(KEYWORD_RET), 1, INT8)
	clean($test)
	copy($test, $text.data, 0, $text[0, $text.top])
	if strstr($test, KEYWORD_RET)
	{
		remove($test)
		Text_reject_position($text)

		remove($self)
		ret(-1)
	}
	$text[0, $text.top]+get_size($test)
	remove($test)
	Text_promise_position($text)
	if is_symbolic(Text_get($text))
	{
		Text_reject_position($text)
		Text_reject_position($text)

		remove($self)
		ret(-2)
	}
	Text_reject_position($text)
	$token=parse_parenthesis($text)
	if $token<0
	{
		$self.value=-1
		Text_resolve_position($text)

		ret($self)
	}
	$self.value=$token
	Text_resolve_position($text)

	ret($self)
}
Ret=new(1, 1, INT8)
Ret.generate=Ret_generate
Ret.show=Ret_show
Ret.remove=Ret_remove
fn Ret_generate($self)
{
	generator_deinit()
	developer_error_not_implemented("function return generation")
}
fn Ret_show($self)
{
	begin_object()
		indent() logf("\"type\": \"Ret\"") next_property()
		indent() Statement_show($self) next_property()
		indent() logf("\"value\": ")
			$value=$self.value
			if $value<0
			{
				logf("null")
			}
			else
			{
				$value.f.show($value)
			}
	end_object()
}
fn Ret_remove($self)
{
	$value=$self.value
	if $value>=0
	{
		$value.f.remove($value)
	}

	remove($self)
}