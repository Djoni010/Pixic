/*
(C) 2022-2023 Серый MLGamer <Seriy-MLGamer@yandex.ru>

This file is part of Pixic.
Pixic is free software: you can redistribute it and/or modify it under the terms of the GNU General Public License as published by the Free Software Foundation, either version 3 of the License, or (at your option) any later version.
Pixic is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU General Public License for more details.
You should have received a copy of the GNU General Public License along with Pixic. If not, see <https://www.gnu.org/licenses/>.
*/

/**
 * @file program/translator/tokens/expressions/Property.pixi
 *
 * Dependencies.
 * program/translator/tokens/expressions/Postfix.pixi
 * program/translator/parser/parsing functions.pixi
 * program/info/syntax errors.pixi
 * program/translator/generator/generator.pixi
 * program/translator/tokens/show.pixi
 */

fn Property_new($text)
{
	$self=Postfix_new(1, 0)
	$self.f=Property

	Text_promise_position($text)
	$self.text=$text.data
	if Text_get($text)!='.'
	{
		Text_reject_position($text)

		remove($self)
		ret(-1)
	}
	if skip_spaces($text)
	{
		$text[1, $text.top]-1
		syntax_error_expected_property($text)
	}
	$self.name_start=$text[0, $text.top]
	Text_promise_position($text)
	$char=Text_get($text)
	if $char>='0'&&$char<='9'
	{
		Text_reject_position($text)
		Text_reject_position($text)

		remove($self)
		ret(-2)
	}
	if is_symbolic($char)
	{
		Text_resolve_position($text)
		Text_promise_position($text)
		while 1
		{
			if is_symbolic(Text_get($text))
			{
				Text_resolve_position($text)
				Text_promise_position($text)
			}
			else
			{
				Text_reject_position($text)
				$self.name_length=$text[0, $text.top]-$self.name_start
				Text_resolve_position($text)

				ret($self)
			}
		}
	}
	Text_reject_position($text)
	syntax_error_expected_property($text)
}
Property=new(1, 1, INT8)
Property.generate=Property_generate
Property.generate_call=Property_generate_call
Property.show=Property_show
Property.remove=Postfix_remove
fn Property_generate($self)
{
	generator_deinit()
	developer_error_not_implemented("property access generation")
}
fn Property_generate_call($self)
{
	generator_deinit()
	developer_error_not_implemented("function call by address")
}
fn Property_show($self)
{
	begin_object()
		indent() logf("\"type\": \"Property\"") next_property()
		indent() logf("\"ID\": ") $self.ID.f.show($self.ID) next_property()
		indent()
			$name=new($self.name_length, 1, INT8)
			copy($name, $self.text, 0, $self.name_start)
			logf("\"name\": \"%s\"", $name)
			remove($name)
	end_object()
}